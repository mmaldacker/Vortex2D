#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

layout (local_size_x_id = 1, local_size_y_id = 2) in;

layout(push_constant) uniform Consts
{
  int width;
  int height;
}consts;

#include "CommonParticles.comp"

layout(std430, binding = 0) buffer Particles
{
  Particle value[];
}particles;

layout(std430, binding = 1) buffer NewParticles
{
  Particle value[];
}newParticles;

layout(std430, binding = 2) buffer Index
{
  int value[];
}scanIndex;

layout(std430, binding = 3) buffer Count
{
  int value[];
}count;

struct DispatchParams
{
    uint x;
    uint y;
    uint z;
    uint count;
};

layout(std430, binding = 4) buffer Params
{
    DispatchParams params;
};

void main()
{
    uvec2 localSize = gl_WorkGroupSize.xy; // Hack for Mali-GPU

    uint index = gl_GlobalInvocationID.x;
    if (index < params.count)
    {
        ivec2 pos = ivec2(particles.value[index].Position);
        if (pos.x >= 0 && pos.x < consts.width && pos.y >= 0 && pos.y < consts.height)
        {
            int particleIndex = pos.x + pos.y * consts.width;
            int particleCount = atomicAdd(count.value[particleIndex], -1) - 1;
            if (particleCount >= 0)
            {
                newParticles.value[scanIndex.value[particleIndex] + particleCount] = particles.value[index];
            }
        }
    }
}
